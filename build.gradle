plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'net.researchgate.release' version '2.8.1'
    id 'com.github.ben-manes.versions' version '0.33.0'
    id 'com.github.breadmoirai.github-release' version '2.2.12'
    id 'com.jfrog.bintray' version '1.8.5'
}

group = 'com.redislabs'
description = 'Spring Batch RediSearch'
sourceCompatibility = '1.8'

repositories {
	jcenter()
	mavenCentral()
	mavenLocal()
}

dependencies {
	compileOnly 'org.projectlombok:lombok:1.18.16'
	annotationProcessor 'org.projectlombok:lombok:1.18.16'
	api 'com.redislabs:lettusearch:2.4.4'
    api 'org.apache.commons:commons-pool2:2.9.0'
	implementation ('org.springframework.boot:spring-boot-starter-batch:2.4.0') {
		exclude group: 'io.lettuce', module: 'lettuce-core'
	}
	testImplementation('org.springframework.boot:spring-boot-starter-test:2.3.4.RELEASE') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.batch:spring-batch-test:4.2.4.RELEASE'
	testImplementation 'org.hsqldb:hsqldb:2.5.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
    testImplementation 'org.testcontainers:testcontainers:1.15.0-rc2'
    testImplementation 'org.testcontainers:junit-jupiter:1.15.0-rc2'
}

test {
	useJUnitPlatform()
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : ''
    key = project.hasProperty('bintrayKey') ? project.property('bintrayKey') : ''
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven'
        name = 'spring-batch-redisearch'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Redislabs-Solution-Architects/spring-batch-redisearch.git'
	    version {
	        gpg {
	            sign = true
	        }
	        mavenCentralSync {
	            sync = true
	            user = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : ''
	            password = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : ''
	        }
	    }
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.redislabs'
            artifactId 'spring-batch-redisearch'

            pom {
                name = 'Spring Batch RediSearch'
                packaging = 'jar'
                description = 'Spring Batch RediSearch'
                url = 'https://github.com/Redislabs-Solution-Architects/spring-batch-redisearch'

		        scm {
		          connection = 'scm:git:git://github.com/Redislabs-Solution-Architects/spring-batch-redisearch.git'
		          developerConnection = 'scm:git:git@github.com:Redislabs-Solution-Architects/spring-batch-redisearch.git'
		          url = 'https://github.com/Redislabs-Solution-Architects/spring-batch-redisearch'
		        }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'jruaux'
                        name = 'Julien Ruaux'
                    }
                }
            }
        }
    }
}

githubRelease {
    token = project.hasProperty('githubToken') ? project.property('githubToken') : ''
    owner "Redislabs-Solution-Architects"
    repo "spring-batch-redisearch"
    body changelog()
}

afterReleaseBuild.dependsOn ":githubRelease"
afterReleaseBuild.dependsOn bintrayUpload